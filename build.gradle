import groovy.json.JsonOutput

buildscript {
    ext {
        assetPipelineVersion = '2.9.0'
    }
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:1.3.3'
        classpath 'com.craigburke.gradle:client-dependencies:1.1.3'
        classpath 'com.craigburke:asciidoctor-asset-pipeline:1.1.0-SNAPSHOT'
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.0'
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:${assetPipelineVersion}"
        classpath 'org.ajoberstar:gradle-git:1.5.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.0'
    id 'com.github.jruby-gradle.base' version '1.2.1'
}

apply plugin: 'io.ratpack.ratpack-groovy'
apply plugin: 'com.craigburke.client-dependencies'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: 'org.ajoberstar.github-pages'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile ratpack.dependency("guice")
    compile "com.bertramlabs.plugins:ratpack-asset-pipeline:${assetPipelineVersion}"
    runtime 'com.craigburke:asciidoctor-asset-pipeline:1.1.0-SNAPSHOT'
    assets 'com.craigburke:asciidoctor-asset-pipeline:1.1.0-SNAPSHOT'
    gems('rubygems:asciidoctor-bespoke:1.0.0.alpha.1') { transitive false }
}

ext {
    githubRepoUri = 'https://github.com/johnrengelman/how-to-shadow.git'
    assetPath = file('src/assets').absolutePath
    assetConfigFile = file('build/resources/main/assets.json')
    asciidoctorConfig = [
        baseDir: "${assetPath}/asciidoc".toString(),
        safe: 'unsafe',
        gemPath: "${assetPath}/vendor/rubygems".toString(),
        backend: 'bespoke',
        requires: [ 'asciidoctor-bespoke', 'asciidoctor-diagram' ],
        templateDirs: [ "${assetPath}/templates".toString() ],
        attributes: [ icons: 'font', imagesoutdir: "${assetPath}/images".toString() ]
    ]
}

jrubyPrepare {
    outputDir asciidoctorConfig.gemPath
}

clientDependencies {
    installDir = "${assetPath}/vendor"

    npm {
        'bespoke'('1.*')
        'bespoke-classes'('1.*', into: 'bespoke/plugins')
        'bespoke-scale'('1.*', into: 'bespoke/plugins')
        'bespoke-keys'('1.*', into: 'bespoke/plugins')
        'bespoke-progress'('1.*', into: 'bespoke/plugins')
        'bespoke-hash'('1.*', into: 'bespoke/plugins')
        'bespoke-backdrop'('1.*', into: 'bespoke/plugins')
        'bespoke-bullets'('1.*', into: 'bespoke/plugins')
        'bespoke-state'('1.*', into: 'bespoke/plugins')
        'bespoke-fullscreen'('1.*', into: 'bespoke/plugins')
        'bespoke-touch'('1.*', into: 'bespoke/plugins')
        'bespoke-overview'('1.*', into: 'bespoke/plugins', transitive: false)
        'normalize.css'('4.1.1')
        'font-awesome'('4.6.3') {
            include 'css/font-awesome.min.css'
            include 'fonts/**'
        }
        'highlightjs'('8.7.0') {
            include 'highlight.pack.min.js'
            include 'styles/darkula.css'
        }
    }
}

assets {
    assetsPath = assetPath
    configOptions = [asciidoctor: asciidoctorConfig]
    excludes = ['**/*.slim', '**/*.rb', '**/slides/*', '**/code/**', '**/rubygems/**']
}

githubPages {
    repoUri = project.githubRepoUri

    credentials {
        username = project.hasProperty('githubToken') ? project.githubToken : ''
        password = ''
    }

    pages {
        from assetCompile.destinationDir
    }
}

task saveAssetConfig() {
    outputs.file assetConfigFile
    doLast {
        assetConfigFile.createNewFile()
        Map ratpackConfig = [ assets:
           [url: '/', sourcePath: assetPath, assets: [asciidoctor: asciidoctorConfig]]
        ]
        assetConfigFile.text = JsonOutput.toJson(ratpackConfig)
    }
}

run.dependsOn saveAssetConfig
assetCompile.dependsOn jrubyPrepare
publishGhPages.dependsOn assetCompile

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
